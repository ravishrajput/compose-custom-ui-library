plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}

project.afterEvaluate {
    publishToMavenLocal {
        publishing {
            publications {
                release(MavenPublication) {
                    groupId LIBRARY_GROUP
                    artifactId LIBRARY_ARTIFACT_ID
                    version LIBRARY_VERSION_NAME

                    if (project.plugins.findPlugin("com.android.library")) {
                        from components.release
                    } else {
                        artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                    }

                    artifact androidSourcesJar
                }
            }
            publications.all {
                pom.withXml {
                    asNode().dependencies.'*'
                            .findAll() {
                                it.scope.text() == 'runtime' &&
                                        project.configurations.implementation.allDependencies.find {
                                            dep -> dep.name == it.artifactId.text()
                                        }
                            }.each { it.scope*.value = 'compile' }
                }
            }
        }

        doLast {
            def prettyPrint = {
                1.upto(100, { print "=" })
                println()
            }
            println()
            prettyPrint()
            println "PUBLICATION FINISHED"
            println "Artifact RELEASE: " + LIBRARY_GROUP + ":" + LIBRARY_ARTIFACT_ID + ":" + LIBRARY_VERSION_NAME
            println "Artifact DEBUG: " + LIBRARY_GROUP + ":" + LIBRARY_ARTIFACT_ID + ":" + LIBRARY_VERSION_NAME
            prettyPrint()
        }
    }
}